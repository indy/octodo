apply plugin: 'android'

repositories {
  mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'de.greenrobot:eventbus:2.0.2'
    compile 'com.google.android.gms:play-services:3.1.36'
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile project(':libraries:ViewPagerIndicator')
}

// version/git/buildtime information from Jake Wharton's post:
// https://plus.google.com/108284392618554783657/posts/6f5TcVPRZij
import java.text.SimpleDateFormat;

def versionMajor = 0
def versionMinor = 1
def versionPatch = 1
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

def gitSha() {
  return 'git rev-parse --short HEAD'.execute().text.trim()
}

def buildTime() {
  def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
  df.setTimeZone(TimeZone.getTimeZone("UTC"))
  return df.format(new Date())
}

android {
    compileSdkVersion 18
    buildToolsVersion "18.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 18

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfig """\
          public static final String GIT_SHA = "${gitSha()}";
          public static final String BUILD_TIME = "${buildTime()}";
          public static final String VERSION = "${versionMajor}.${versionMinor}.${versionPatch}";
        """
    }
}
